###**
Params
======

class up.params
###
up.params = (($) ->
  u = up.util

  NATURE_MISSING = 0
  NATURE_ARRAY = 1
  NATURE_QUERY = 2
  NATURE_FORM_DATA = 3
  NATURE_OBJECT = 4

  natureOf = (params) ->
    if u.isMissing(params)
      NATURE_MISSING
    else if u.isArray(params)
      NATURE_ARRAY
    else if u.isString(params)
      NATURE_QUERY
    else if u.isFormData(params)
      NATURE_FORM_DATA
    else if u.isObject(params)
      NATURE_OBJECT
    else
      up.fail("Unsupport params type: %o", params)

  ###**
  Returns an array representation of the given `params`.

  Each array element will be an object with `{ name }` and `{ value }` properties.

  @function up.params.toArray
  ###
  toArray = (params) ->
    switch natureOf(params)
      when NATURE_MISSING
        []
      when NATURE_ARRAY
        params
      when NATURE_QUERY
        buildArrayFromQuery(params)
      when NATURE_FORM_DATA
        buildArrayFromFormData(params)
      when NATURE_OBJECT
        buildArrayFromObject(params)

  ###**
  Returns an object representation of the given `params`.

  @function up.params.toObject
  ###
  toObject = (params) ->
    switch natureOf(params)
      when NATURE_MISSING
        {}
      when NATURE_ARRAY, NATURE_QUERY, NATURE_FORM_DATA
        buildObjectFromArray(toArray(params))
      when NATURE_OBJECT
        params

  ###**
  Returns [`FormData`](https://developer.mozilla.org/en-US/docs/Web/API/FormData) representation of the given `params`.

  @function up.params.toFormData
  ###
  toFormData = (params) ->
    switch natureOf(params)
      when NATURE_MISSING
        # Return an empty FormData object
        new FormData()
      when NATURE_ARRAY, NATURE_QUERY, NATURE_OBJECT
        buildFormDataFromArray(toArray(params))
      when NATURE_FORM_DATA
        params

  ###**
  Returns an query string for the given `params`.

  @function up.params.toQuery
  ###
  toQuery = (params) ->
    switch natureOf(params)
      when NATURE_MISSING
        ''
      when NATURE_QUERY
        params
      when NATURE_ARRAY, NATURE_FORM_DATA, NATURE_OBJECT
        buildQueryFromArray(toArray(params))

  arrayEntryToQuery = (entry) ->
    value = entry.value
    return undefined unless isPrimitiveValue(value)
    query = encodeURIComponent(entry.name)
    # There is a subtle difference when encoding blank values:
    # 1. An undefined or null value is encoded to `key` with no equals sign
    # 2. An empty string value is encoded to `key=` with an equals sign but no value
    if u.isGiven(value)
      query += "="
      query += encodeURIComponent(value)
    query

  ###**
  Returns whether the given value can be encoded into a query string.

  We will have `File` values in our params when we serialize a form with a file input.
  These entries will be filtered out when converting to a query string.
  ###
  isPrimitiveValue = (value) ->
    u.isMissing(value) || u.isString(value) || u.isNumber(value) || u.isBoolean(value)

  safeSet = (obj, k, value) ->
    obj[k] = value  unless u.isBasicObjectProperty(k)

#  safeGet = (obj, k) ->
#    obj[k] unless u.isBasicObjectPropery(k)

  buildQueryFromArray = (array) ->
    parts = u.map(array, arrayEntryToQuery)
    parts = u.compact(parts)
    parts.join('&')

  buildArrayFromQuery = (query) ->
    array = []
    for part in query.split('&')
      if part
        [name, value] = part.split('=')
        name = decodeURIComponent(name)
        # There are three forms we need to handle:
        # (1) foo=bar should become { name: 'foo', bar: 'bar' }
        # (2) foo=    should become { name: 'foo', bar: '' }
        # (3) foo     should become { name: 'foo', bar: null }
        if u.isGiven(value)
          value = decodeURIComponent(value)
        else
          value = null
        array.push({ name, value })
    array

  buildArrayFromObject = (object) ->
    array = []
    for k, v of object
      array.push(name: k, value: v)
    array

  buildObjectFromArray = (array) ->
    obj = {}
    for entry in array
      safeSet(obj, entry.name, entry.value)
    obj

  buildArrayFromFormData = (formData) ->
    array = []
    u.eachIterator formData.entries(), (value) ->
      [name, value] = value
      array.push({ name, value })
    array

  buildFormDataFromArray = (array) ->
    formData = new FormData()
    for entry in array
      formData.append(entry.name, entry.value)
    formData

  buildURL = (base, params) ->
    parts = [base, toQuery(params)]
    parts = u.compact(parts)
    separator = if u.contains(base, '?') then '&' else '?'
    parts.join(separator)

  ###**
  Adds to the given `params` a new  entry with the given `name` and `value`.

  The given `params` is changed in-place, if possible.
  The return value is always a params value that includes the new entry.

  @function up.params.add
  @param {string|object|FormData|Array|undefined} params
  @param {string} name
  @param {any} value
  @return {string|object|FormData|Array}
  ###
  add = (params, name, value) ->
    newEntry = [{ name, value }]
    assign(params, newEntry)

  ###**
  Returns a new params value that contains entries from both `params` and `otherParams`.

  The given `params` argument is not changed.

  @function up.params.merge
  @param {string|object|FormData|Array|undefined} params
  @param {string|object|FormData|Array|undefined} otherParams
  @return {string|object|FormData|Array}
  ###
  merge = (params, otherParams) ->
    switch natureOf(params)
      when NATURE_MISSING
        merge({}, otherParams)
      when NATURE_ARRAY
        otherArray = toArray(otherParams)
        params.concat(otherArray)
      when NATURE_FORM_DATA
        formData = new FormData()
        assign(formData, params)
        assign(formData, otherParams)
        formData
      when NATURE_QUERY
        otherQuery = toQuery(otherParams)
        parts = u.select([params, otherQuery], u.isPresent)
        parts.join('&')
      when NATURE_OBJECT
        u.merge(params, toObject(otherParams))

#  ###**
#  Returns the param value with the given name.
#
#  @function up.params.get
#  ###
#  get = (params, name) ->
#    switch natureOf(params)
#      when NATURE_MISSING
#        undefined
#      when NATURE_ARRAY
#        u.detect params, (entry) -> entry.name == name
#      when NATURE_FORM_DATA
#        params.get(name)
#      when NATURE_QUERY
#        safeGet(toObject(params), name)
#      when NATURE_OBJECT
#        safeGet(params, name)

  ###**
  Extends the given `params` with entries from the given `otherParams`.

  The given `params` is changed in-place, if possible.
  The return value is always a params value that includes the new entries.

  @function up.params.assign
  @param {string|object|FormData|Array|undefined} params
  @param {string|object|FormData|Array|undefined} otherParams
  @return {string|object|FormData|Array}
  ###
  assign = (params, otherParams) ->
    switch natureOf(params)
      when NATURE_ARRAY
        otherArray = toArray(otherParams)
        params.push(otherArray...)
        params
      when NATURE_FORM_DATA
        otherArray = toArray(otherParams)
        for entry in otherArray
          params.append(entry.name, entry.value)
        params
      when NATURE_OBJECT
        u.assign(params, toObject(otherParams))
      when NATURE_QUERY, NATURE_MISSING
        # Strings and undefined are immutable, so we merge instead.
        merge(params, otherParams)

  submittingButton = (form) ->
    $form = $(form)
    submitButtonSelector = up.form.submitButtonSelector()
    $activeElement = $(document.activeElement)
    if $activeElement.is(submitButtonSelector) && $form.has($activeElement)
      $activeElement[0]
    else
      # If no button is focused, we assume the first button in the form.
      $form.find(submitButtonSelector)[0]

  ###**
  Extracts request params from the given '<form>`.

  @function up.params.fromForm
  @param {Element|jQuery|string} form
  @return {Object}
  ###
  fromForm = (form) ->
    if form = u.element(form)
      fd = new FormData(form)
      if button = submittingButton(form)
        fd.append(button.name, button.value)
      return fd

  ###**
  Returns the [query string](https://en.wikipedia.org/wiki/Query_string) from the given URL.

  The query string is returned without a leading question mark (`?`).

  Returns `undefined` if the given URL has no query component.

  @function up.params.fromURL
  @param {string} url
  @return {string|undefined}
  @experimental
  ###
  fromURL = (url) ->
    urlParts = u.parseUrl(url)
    if query = urlParts.search
      query = query.replace(/^\?/, '')
      query

  toArray: toArray
  toObject: toObject
  toQuery: toQuery
  toFormData: toFormData
  buildURL: buildURL
#  get: get
  add: add
  assign: assign
  merge: merge
  fromForm: fromForm
  fromURL: fromURL

)(jQuery)
